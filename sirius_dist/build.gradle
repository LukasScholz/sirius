import org.gradle.api.internal.file.copy.CopyAction
import org.gradle.api.internal.file.copy.FileCopyAction
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption

plugins {
    id 'org.beryx.runtime' version '1.11.4'
}

ext {
    OS = DefaultNativePlatform.currentOperatingSystem

    if (OS.isLinux()) {
        nativeLibs = 'l64'
        osName = 'linux64'
        installerNameType = 'deb'
        installerAppendix = "_${version}-1_amd64.$installerNameType"
    } else if (OS.isWindows()) {
        nativeLibs = 'w64'
        osName = 'win64'
        installerNameType = 'msi'
        installerAppendix = "_${version.replace('-SNAPSHOT', '')}.$installerNameType"
    } else if (OS.isMacOsX()) {
        nativeLibs = 'osx'
        osName = 'osx64'
        installerNameType = 'pkg'
        installerAppendix = "-${version}.$installerNameType"
    }
}
subprojects {
    apply plugin: 'ManifestClasspath'
    //this is to keep classpath away from commandline because it may get too long for windows
    apply plugin: 'org.beryx.runtime'


    runtime {
        options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
        modules = [
                'java.sql',
//                'javafx.base',
                'java.desktop',
//                'javafx.web',
//                'javafx.graphics',
                'java.logging',
//                'javafx.swing',
                'jdk.jsobject',
                'java.xml',
                'jdk.xml.dom',
                'java.datatransfer',
                'java.naming',
                'java.prefs',
//                'javafx.controls'
        ]

        targetPlatform(osName) {
            if (OS.isLinux()) {
                jdkHome = jdkDownload("https://cdn.azul.com/zulu/bin/zulu15.27.17-ca-fx-jdk15.0.0-linux_x64.tar.gz")
            } else if (project.OS.isWindows()) {
                jdkHome = jdkDownload("https://cdn.azul.com/zulu/bin/zulu15.27.17-ca-fx-jdk15.0.0-win_x64.zip")
            } else if (project.OS.isMacOsX()) {
                jdkHome = jdkDownload("https://cdn.azul.com/zulu/bin/zulu15.27.17-ca-fx-jdk15.0.0-macosx_x64.tar.gz") /*{
                    pathToHome = "zulu14.29.23-ca-fx-jdk14.0.2-macosx_x64/zulu-14.jdk/Contents/Home"
                }*/
            } else {
                throw new RuntimeException("Could not detect OS for packaging!")
            }
        }

        jpackage {
            targetPlatformName = osName
            installerType = installerNameType

            outputDir = "distributions"

            installerOptions = [
                    '--verbose',
                    '--vendor', 'FSU Jena',
                    '--license-file', "${project.getRootProject().projectDir}/jpackage/LICENSE-Full.txt",
                    '--description', 'SIRIUS is a java-based software framework for discovering a landscape of de-novo identification of metabolites using tandem mass spectrometry.',

            ]
            if (OS.isLinux()) {
                //todo rpm check
                installerOptions += [
                        '--app-version', "${project.version}",
//                        '--linux-package-name', 'sirius',
                        '--linux-menu-group', 'Applications;Science',
                        '--linux-deb-maintainer', 'sirius@uni-jena.de',
                ]
                println("########### ${project.getRootProject().projectDir}/icons/sirius-icon.png")
                imageOptions += [
                        '--icon', "${project.getRootProject().projectDir}/icons/sirius-icon.png",
                ]
            } else if (OS.isWindows()) {
                installerOptions += [
                        '--app-version', "${project.version.replace('-SNAPSHOT', '')}",
                        '--win-dir-chooser',
                        '--win-menu',
//                    '--win-shortcut',
                        '--win-menu-group', 'SIRIUS'
//                    '--win-per-user-install',
                ]
                imageOptions += [
                        '--icon', "${project.getRootProject().projectDir}/icons/sirius-icon.ico",
                ]
            } else if (OS.isMacOsX()) {
                installerOptions += [
                        '--app-version', "${project.version}",
                ]
                imageOptions += [
                        '--icon', "${project.getRootProject().projectDir}/icons/sirius-icon.icns",
                ]
            }
        }
    }

    jpackageImage.doLast {
        File nativeLibsTarget
        File appRootTarget
        if (OS.isLinux()) {
            appRootTarget = jpackageImage.outputs.files.singleFile.toPath().resolve(jpackageImage.getJpackageData().imageName).toFile()
            nativeLibsTarget = appRootTarget.toPath().resolve("lib/app").toFile()
        } else if (OS.isWindows()) {
            appRootTarget = jpackageImage.outputs.files.singleFile.toPath().resolve(jpackageImage.getJpackageData().imageName).toFile()
            nativeLibsTarget = appRootTarget
        } else if (OS.isMacOsX()) {
            appRootTarget = jpackageImage.outputs.files.singleFile.toPath().resolve("${jpackageImage.getJpackageData().imageName}.app/Contents").toFile()
            nativeLibsTarget = appRootTarget.toPath().resolve("app").toFile()
        } else {
            throw new RuntimeException("Could not detect OS for packaging!")
        }
        copy {
            from("${project.glpkPath}/$nativeLibs/") {
                include('libglpk.so.40')
                include('libglpk_java.so')
            }
            from("${project.clpPath}/$nativeLibs/")
            into(nativeLibsTarget)
        }
        copy {
            from("${project.distPath}") {
                include('COPYING.txt')
                include('LICENSE.txt')
            }
            from(rootProject.downloadManual.outputs.files.singleFile)
            into(appRootTarget)
        }
    }

    jpackageImage.doFirst {
        println("===================>>>>>>>>>>" + runtime.jreDir.file("$project.name-$osName").get())
        runtime.jreDir.file("$project.name-$osName").get().asFile
                .renameTo(runtime.jreDir.file("${jpackageImage.getJpackageData().installerName}-$osName").get().asFile)
    }

    jpackageImage.dependsOn ':downloadGLPK'
    jpackageImage.dependsOn ':downloadCLP'
    jpackageImage.dependsOn ':downloadManual'

    task jpackageImageZip(dependsOn: jpackageImage) {
        group = 'build'
        doFirst {
            def imageDir = jpackageImage.outputs.files.singleFile.toPath().resolve("${jpackageImage.getJpackageData().imageName}${OS.isMacOsX()?'.app':''}").toFile()
            def parentPath = imageDir.parentFile.toPath()
            def zipFile = jpackageImage.outputs.files.singleFile.toPath().resolve("${jpackageImage.getJpackageData().imageName}-${project.version}-${osName}.zip").toFile()
            project.ant.zip(destfile: zipFile, duplicate: 'fail') {
                imageDir.eachFileRecurse { f ->
                    int mode = f.canExecute() ? 755 : 644
                    def relPath = parentPath.relativize(f.toPath()).toString()
                    zipfileset(dir: parentPath, includes: relPath, filemode: mode)
                }
            }
        }
    }

    task distribution { group = 'distribution' }

    distribution.dependsOn 'jpackage'
    distribution.dependsOn 'jpackageImageZip'
    artifactoryPublish.dependsOn 'distribution'

}
